// COPYRIGHT (c) 2001-2025 RDC Tools International
//
// This program is free software; you can redistribute it and/or
// modify it under the terms of the GNU General Public License.

// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// You should have received a copy of the GNU General Public License
// along with this program; (See the GPL.txt file in the Help directory) if not, write to the Free Software
// Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.
//
Use DFAllEnt.pkg
Use cRDCCJCommandBarSystem.pkg

Use cDigitalSoftwareCertificate.pkg
Object oDigitalSoftwareCertificate is a cDigitalSoftwareCertificate
    Move Self to ghoDigitalSoftwareCertificate

    // Set pbWarnIfNotDigitallySigned = True if you like to have a warning when the
    // program is started, but the program has not been digitally signed.
    // Only applies when run outside the Studio.
    Set pbWarnIfNotDigitallySigned to False

    // This message does the actual checking for a valid digital certificate.
    // If the program has been tampered with e.g. it has been infected by a virus,
    // the user will get a warning and the program will be aborted.
//    Send ValidateProgramOnStartup

End_Object

Object oHtmlHelp is a cHtmlHelp
    Set pbAlwaysOnTop to False
End_Object

//Compiler_Desktop_Method_Warnings On
Use cRDCApplication.pkg
Object oApplication is a cRDCApplication
    Set psCompany  to "RDC Tools International"
    Set psProduct  to "CrossMerge"
    Set psProgram  to "Builder"
    Set psHelpFile to "CrossMerge.chm"
    Set pbPreserveEnvironment to True

    Property Handle phoMain_DDO  
    Property Handle phoMainView
    Property Boolean pbCMBuilderMode True

    Property String psSerial ""
    Property String psName ""
    Property String psLicenseType "FREE VERSION"
    Property String psOrgCollectionName ""
    Property String psDefaultCollection ""
    Property String psGridRowBackgroundColor ""
    
    Procedure OnWorkspaceOpened
        String sParam
        Handle hoCmdLine
        
        Forward Send OnWorkspaceOpened
        
        Get phoCommandLine to hoCmdLine
        Get Argument of hoCmdLine 1 to sParam
        If (lowercase(sParam) = "/h" or Lowercase(sParam) = "-h" or Lowercase(sParam) = "f1" or Lowercase(sParam) contains "help") Begin
            Send DoDisplayTOC of ghoHtmlHelp
        End
    End_Procedure
    
    Object oConnection is a cConnection
        Use LoginEncryption.pkg
        Use DatabaseLoginDialog.dg
    End_Object

End_Object

Use Startup.pkg
Use cReindex.pkg
Object oReindex is a cReindex // Checks if tables needs reindexing.
    Set piCheckFile to 81
End_Object

Use cDbUpdateHandler.pkg
Object oDbUpdateHandler is a cDbUpdateHandler
    Set pbAutoCreateDbVersionTable to True  // Automatically creates the DbVersion table when the program is started.
    Set piDbVersionFileNumber  to 249
    Set piDbVersionFieldNumber to 1
    Set pbUseIntFilesBackup to True
    Set piIntFilesFileNumber to 250
    Set pbCheckDataFlexUserCount to False
    Set psDriverID to DATAFLEX_ID
    Set piDbType to EN_DbTypeDataFlex
    Set pbExitIfDebuggerActive to False
    
    Use DUF_MultipleTables1_1.pkg

    Procedure OnPostUpdate
        Integer iRetval iRecords    
        Forward Send OnPostUpdate

        Open SncTable
        Get_Attribute DF_FILE_RECORDS_USED of SncTable.File_Number to iRecords
        If (iRecords <> 0) Begin
            Get YesNo_Box "The logic has changed a lot for this revision. Therefor all data needs to be deleted and you will need to create your data connections again. Continue to delete records? (else program will be aborted)" to iRetval
            If (iRetval = MBR_No) Begin
                Send Exit_Application
            End
            ZeroFile SncTable 
            Open SncSys
            ZeroFile SncSys
            Open SncLog
            ZeroFile SncLog
            Open SncSchem
            ZeroFile SncSchem
            Open SncTHea
            ZeroFile SncTHea
            Open SncTRow
            ZeroFile SncTRow 
//            Open SncSchemRow
//            ZeroFile SncSchemRow 
        End
    End_Procedure

End_Object

Send Ignore_Error of Error_Object_Id DFERR_CANT_LOAD_DLL

Use CMStatPnl.pkg
Use cRDCTooltipController.pkg
Object oToolTipController is a cRDCTooltipController
End_Object

Use oEditContextMenu.pkg
Use oDEOEditContextMenu17.pkg
Use cRDCHeaderDbGroup.pkg

Object oMain is a Panel
    Set Label to (psProduct(ghoApplication) * psProgram(ghoApplication) * " - " + psLicenseType(ghoApplication))
    Set Location to 4 0
    Set Size to 439 544
    Set piMinSize to 439 544
    Set Icon to "CmBuilder.ico"

    Use ChecklistTest.dg

    Object oCommandBarSystem is a cRDCCJCommandBarSystem

        Object oFindToolBar is a cCJToolbar
            Set psTitle to "Finding Toolbar"

            Object oFindFirstTool is a cCJFindFirstMenuItem
            End_Object

            Object oFindPreviousTool is a cCJFindPreviousMenuItem
            End_Object

            Object oFindMenuTool is a cCJFindMenuItem
            End_Object

            Object oFindNextTool is a cCJFindNextMenuItem
            End_Object

            Object oFindLastTool is a cCJFindLastMenuItem
            End_Object

            Object oPromptToolItem is a cCJPromptMenuItem
                Set pbControlBeginGroup to True
            End_Object

        End_Object

        Object oFileToolBar is a cCJToolbar
            Set psTitle to "Data Entry Toolbar"

            Object oClearToolItem is a cCJClearMenuItem
                Set pbControlBeginGroup to True
            End_Object

            Object oClearAllToolItem2 is a cCJClearAllMenuItem
            End_Object

            Object oSaveToolItem is a cCJSaveMenuItem
                Set pbControlBeginGroup to True
            End_Object

            Object oDeleteToolItem is a cCJDeleteMenuItem
            End_Object

        End_Object

        Object oCmEngine_CJToolbar is a cCJToolbar
            Object oCmEngine_MenuItem is a cCJMenuItem
                Set psCaption to "CrossMerge Engine"
                Set psDescription to "Start or Configure the CrossMerge Engine program"
                Set psToolTip to "Start or Configure the CrossMerge Engine program"
                Set psImage to "CmEngine.ico"
                Set peControlStyle to xtpButtonIconAndCaption
                Set peControlType to xtpControlPopup

                Procedure OnExecute Variant vCommandBarControl
                    Boolean bSave
                    String sPath sProgram
                    Integer iRetval

                    Get Should_Save of (Main_DD(phoMainView(ghoApplication))) to bSave
                    If (bSave = False) Begin
                        Get Should_Save of (oSncSys_DD(phoMainView(ghoApplication)))   to bSave
                    End
                    If (bSave = False) Begin
                        Get Should_Save of (oSncSchem_DD(phoMainView(ghoApplication))) to bSave
                    End
                    If (bSave = True) Begin
                        Get YesNo_Box "Changes exist. Save them first?" to iRetval
                        If (iRetval = Mbr_Yes) Begin
                            Send Request_Save of (phoMainView(ghoApplication))
                        End
                    End
                    Move (psProgramPath(phoWorkspace(ghoApplication))) to sPath
                    Move "CMEngine" to sProgram
                    #IFDEF IS$WIN64
                        Append sProgram "64"
                    #ENDIF    
                    Append sProgram ".exe"
                    Send vShellExecute "open" sProgram "" sPath
                End_Procedure

                Object oCmEngineMenuItemSub is a cCJMenuItem
                    Set psCaption to "Start CrossMerge Engine"
                    Set psDescription to "Start the CrossMerge Engine program"
                    Set psToolTip to "Start the CrossMerge Engine program"
                    Set psImage to "CmEngine.ico"

                    Procedure OnExecute Variant vCommandBarControl
                        Boolean bSave
                        String sPath sProgram
                        Integer iRetval
    
                        Get Should_Save of (Main_DD(phoMainView(ghoApplication))) to bSave
                        If (bSave = False) Begin
                            Get Should_Save of (oSncSys_DD(phoMainView(ghoApplication)))   to bSave
                        End
                        If (bSave = False) Begin
                            Get Should_Save of (oSncSchem_DD(phoMainView(ghoApplication))) to bSave
                        End
                        If (bSave = True) Begin
                            Get YesNo_Box "Changes exist. Save them first?" to iRetval
                            If (iRetval = Mbr_Yes) Begin
                                Send Request_Save of (phoMainView(ghoApplication))
                            End
                        End
                        Move (psProgramPath(phoWorkspace(ghoApplication))) to sPath
                        Move "CMEngine" to sProgram
                        #IFDEF IS$WIN64
                            Append sProgram "64"
                        #ENDIF    
                        Append sProgram ".exe"
                        Send vShellExecute "open" sProgram "" sPath
                    End_Procedure

                End_Object

                Object oCrossMergeEngineCollectionsMenuItem is a cCJMenuItem
                    Set psCaption to "CrossMerge Engine Collections"
                    Set psTooltip to "CrossMerge Engine Collections"
                    Set psImage to "CollectionDetails.ico"
                    Set pbControlBeginGroup to True

                    Procedure OnExecute Variant vCommandBarControl
                        Send Activate_oRdcViewCollections of (Client_Id(phoMainPanel(ghoApplication)))
                    End_Procedure
                End_Object

                Object oSetupCmEngine_MenuItem is a cCJMenuItem
                    Set psCaption to "Con&figure CrossMerge Engine"
                    Set psToolTip to "Configure"
                    Set psDescription to "Configure the CrossMerge Engine Program."
                    Set psImage to "Configure.ico"
                    Procedure OnExecute Variant vCommandBarControl
                        Forward Send OnExecute vCommandBarControl
                        Send Popup of (oCmEngineSettings_dg(Client_Id(ghoCommandBars)))
                    End_Procedure
                End_Object

            End_Object

        End_Object

        Object oHelp_Toolbar is a cCJToolbar
            Set psTitle to "Help ToolBar"
            Set peStretched to stStretchShared

            Object oViewLogFile_MenuItem is a cCJMenuItem
                Set psCaption to "View Logfile"
                Set psToolTip to "View Log records created by the CrossMerge Engine"
                Set psDescription to "View Log records created by the CrossMerge Engine"
                Set psImage to "Report.ico"
                Set pbControlBeginGroup to True
                Procedure OnExecute Variant vCommandBarControl
                    Forward Send OnExecute vCommandBarControl
                    Send Deactivate of (oConnectViewLog(Client_Id(ghoCommandBars)))
                    Send Popup_Modal of (oConnectViewLog(Client_Id(ghoCommandBars)))
                End_Procedure
            End_Object

            Object oMenuItem is a cCJMenuItem
                Set psToolTip to "Toggle Checklist panel"
                Set psImage to "CheckList.ico"
                Set pbControlBeginGroup to True

                Procedure OnExecute Variant vCommandBarControl
                    Send ToggleChecklistPanel of ghoChecklistPanel
                End_Procedure

            End_Object

            Object oSetup_MenuItem is a cCJMenuItem
                Set psCaption to "Con&figure"
                Set psToolTip to "Configure"
                Set psDescription to "Configure the CrossMerge Builder Program. Change settings for the program"
                Set psImage to "Configure.ico"
                Set pbControlBeginGroup to True
                Procedure OnExecute Variant vCommandBarControl
                    Forward Send OnExecute vCommandBarControl
                    Send Popup of (oPropertiesNew_dg(Client_Id(ghoCommandBars)))
                End_Procedure
            End_Object

            Object oAbout_MenuItem is a cCJMenuItem
                Set psCaption to "A&bout"
                Set psDescription to "About the program"
                Set psToolTip to "About"
                Set psImage to "ActionAbout1.ico"
                Set pbControlBeginGroup to True
                Procedure OnExecute Variant vCommandBarControl
                    Forward Send OnExecute vCommandBarControl
                    Send Activate_About of (Client_Id(ghoCommandBars))
                End_Procedure
            End_Object

            Object oKoFi_MenuItem is a cCJMenuItem
                Set psToolTip to "Coffee time!"
                Set psDescription to "Bye me a coffee, to support me"
                Set psImage to "Ko-fi.ico"
                Set pbControlBeginGroup to True
                Procedure OnExecute Variant vCommandBarControl
                    Runprogram Shell Background "https://ko-fi.com/nilss"
                End_Procedure
            End_Object

            Object oSuggestion_MenuItem is a cCJMenuItem
                Set psTooltip to "Suggest"
                Set psDescription to "Suggest a feature, or report a bug..."
                Set psImage to "Suggestion.ico"
                Set pbControlBeginGroup to True

                Procedure OnExecute Variant vCommandBarControl
                    Runprogram Shell Background "https://github.com/NilsSve/CrossMerge/issues/1"
                End_Procedure

            End_Object

            Object oHelpMenuItem is a cCJHelpMenuItem
                Set peControlType to xtpControlSplitButtonPopup
                Set psCaption to "&Help"
                Set psImage to "ActionHelp1.ico"
                Procedure OnExecute Variant vCommandBarControl
                    Forward Send OnExecute vCommandBarControl
                    Send Help
                End_Procedure

                Object oHelpMenuItemLocal is a cCJMenuItem
                    Set psCaption to "Local Help"
                    Set psImage to "ActionHelp1.ico"
                    Procedure OnExecute Variant vCommandBarControl
                        Send Help
                    End_Procedure
                End_Object

                Object oHelpMenuItemInternetBrowser is a cCJMenuItem
                    Set psCaption to "Online HTML Help"
                    Set psImage to "ActionHelpInternet.ico"
                    Procedure OnExecute Variant vCommandBarControl
                        Runprogram Shell Background "http://www.rdctools.com/HTMLHelpCMOS/CrossMerge.html"
                    End_Procedure

                End_Object
                
            End_Object

            Object oExitMenuItem is a cCJExitMenuItem
                Set psImage to "Exit.ico"
                Set psToolTip to "Exit the program (Alt+F4)"
                Set pbControlBeginGroup to True
            End_Object

        End_Object

        Object oStatusBar is a cCJStatusBar

            Object oStatusPane1 is a cCJStatusBarPane
                Set piID to sbpIDIdlePane
                Set pbStyleStretch to True
            End_Object

            Object oStatusPane2 is a cCJStatusBarPane
                Set phoViewPane to Self
                Set pbStyleStretch to True
            End_Object

        End_Object

    End_Object

    Object oClientArea is a ClientArea
//        Use Dd_debug.dg
        
        Use xxSoftwareKey.pkg     // Modified Software key plus software protection package.
        Use Working.pkg           // RDC "Working..." panel object
        Use SyncFuncs.pkg         // Misc. global functions and other stuff.
        Use DFSYesNoCancel.dg     // Better data_loss message. Yes/No/Cancel.
        
        Use ViewData.dg
        Use Password.dg
        Use DeletePassword.dg
        Use Append.dg
        Use PropertiesNew.dg
        Use CmEngineSettings.dg
        Use SelectSourceAndTargetFields.dg
        Use SelectIndexAndSegments.dg
        Use CmBuilder.vw
        Use CmBuilderCollections.vw
        Use ViewLogFile.vw
        
        Use StdAbout.pkg
        Procedure Activate_About
            Send DoAbout "" "" ("Copyright 2001-2025" * psCompany(ghoApplication)) "Created By: Nils Svedmyr" "CmBuilder.bmp" "Website: http://www.rdctools.com" "E-mail: mailto:support@rdctools.com" "This is a FREE PROGRAM WITH NO SUPPORT. Send an email to the address above TO BUY A LICENSE WITH LIFETIME SUPPORT AND UPDATES for ONLY $395." "'CrossMerge', 'CrossMerge Builder' and 'CrossMerge Engine' are trademarks of RDC Tools International." ("This tool was created with DataFlex" * String(C_DFVersion) * "from Data Access Worldwide. Please visit http://www.dataaccess.com for a free personal copy of DataFlex.")
        End_Procedure

        // We do not allow closing of a view - it just doesn't make
        // sense with a tabbed interface.
        Function Exit_Loss_Confirmation For cUIObject Returns Integer
            Function_Return 1
        End_Function

        Function Verify_Exit_Application Returns Integer
            Integer iRetval
            Boolean bChanged bStay
            Handle ho

            Get phoMainView of ghoApplication to ho
            Get Should_Save of ho to bChanged
            If (bChanged = False) Begin
                Move False to bStay
            End

            Else Begin
                Get DFSYesNoCancel_Box "There are unsaved changes. What would you like to do?" to iRetval
                If (iRetval = MBR_Yes) Begin
                    Send Request_Save to ho
                    Move False to bStay
                End
                If (iRetval = MBR_No) Begin
                    Move False to bStay
                End
                If (iRetval = MBR_Cancel) Begin
                    Move True to bStay
                End
            End             
            If (bStay = False) Begin
                // We need to destroy the skin framework here,
                // else the program will GPF if exited from the 
                // oRdcViewCollections view. Weird, but this fixes it.
                If (ghoSkinFramework <> 0) Begin
                    Send Destroy of ghoSkinFramework
                End
            End
            Function_Return bStay
        End_Function

    End_Object

    Procedure End_Construct_Object
        Forward Send End_Construct_Object
        Set Icon to "CmBuilder.ico"
    End_Procedure

    On_Key Key_Ctrl+Key_Tab           Send Switch_Next_View
    On_Key Key_Ctrl+Key_Shift+Key_Tab Send Switch_Prior_View
End_Object

If (SncSys.UseLogin = 1) Begin
    Send Popup of (oPassword_dg(Client_Id(phoMainPanel(ghoApplication))))
End

Send Destroy of ghoSplashScreen
Send Activate of (phoMainView(ghoApplication))    
//Send Switch_Next_View of (Client_Id(phoMainPanel(ghoApplication)))

Start_UI
