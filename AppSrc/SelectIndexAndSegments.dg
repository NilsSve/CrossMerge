Use cRDCDbModalPanel.pkg
Use cRDCTextbox.pkg
Use cRDCDbForm.pkg
Use cDbIndexComboForm.pkg
Use cViewDataButton.pkg
Use cParentFieldsTreeview.pkg
Use cChildFieldsTreeView.pkg
Use cCommandLinkButton.pkg

Object oSelectIndexAndSegments is a cRDCDbModalPanel
    Set Label to "Select Index And Segments"
    Set Size to 351 491
    Set piMinSize to 351 491
    Set piMaxSize to 600 491
    Set Location to 2 0
    Set Icon to "TableIndex.ico"
    Set Locate_Mode to CENTER_ON_PARENT
    
    Property Handle Main_DD
    Property Handle Server
    Property Boolean pbOK False

    Object oInfoHeader_tb is a cRDCTextbox
        Set Auto_Size_State to False
        Set Size to 12 129
        Set Location to 4 12
        Set Label to "Target Database Index"
        Set FontPointHeight to 12
        Set Justification_Mode to JMode_Center
    End_Object

    Object oSnctable_TodataTable_Index is a cRDCDbForm
        Entry_Item Snctable.Todatatable
        Set Label to "Table Name"
        Set Size to 13 150
        Set Location to 40 12
        Set Label_Col_Offset to 0
        Set Label_Row_Offset to 1
        Set Label_Justification_Mode to JMode_Top
        Set Enabled_State to False

        Procedure Set Enabled_State Boolean bState
            Forward Set Enabled_State to False
        End_Procedure
    End_Object

    Object oSnctable_Toindex is a cDbIndexComboform
        Entry_Item Snctable.Toindex
        Set Label to "Select Unique Index"
        Set Size to 13 150
        Set Location to 40 170
        Set Form_Border to 0
        Set Label_Row_Offset to 1
        Set Label_Col_Offset to 0
        Set Label_Justification_Mode to JMode_Top
        Set Entry_State to False
        Set Combo_Sort_State to True
        Set Code_Display_Mode to cb_Code_Display_Both
        Set pbFrom to False
        Set Server to (Main_DD(Self))

        Procedure OnChange
            Forward Send OnChange
            Send DoCreateTree of oChildFromIndexFields_tv
            Send OnCreateTree of oToIndexFields_tv
        End_Procedure
    End_Object

    Object oViewToData_bn is a cViewDataButton
        Set Label to "View &Data"
        Set Size to 13 60
        Set Location to 40 327
        Set Status_Help to "View data for the Target database table."
        Set Enabled_State to False
        Set pbFrom to False
        Set psImage to "ViewData.ico"

        Procedure OnClick
            Handle hoDD
            Get Main_DD to hoDD
            Set phoDD to hoDD
            Send DoViewData True -1
        End_Procedure
    End_Object

    Object oInfo_tb is a cRDCTextbox
        Set Label to "Select fields to populate the Target table's index, for a Find Equal. This is used for finding an existing Target record, and to update data if found."
        Set Auto_Size_State to False
        Set Location to 77 74
        Set Size to 55 80
        Set Justification_Mode to jMode_Left
        Set peAnchors to anTopRight
        Set FontWeight to fw_Bold
    End_Object

    Object oParentFromFields_tb is a cRDCTextbox
        Set Label to "Available Source Fields:"
        Set Location to 63 184
        Set Size to 10 79
        Set FontWeight to fw_Bold
    End_Object

    Object oParentFromIndexFields_tv is a cParentFieldsTreeView
        Set pbFrom to True
        Set peAnchors to anTopBottom
        Set Size to 107 150
        Set Location to 75 170
        Set Status_Help to "Double-click or press space-bar or the Ins key to select a Source field"

        Procedure DoUpdateData Integer iFromFile Integer iToFile Boolean bFrom
            Set piFile to iFromFile
        End_Procedure
    End_Object

    Object oCopyFromToBelow_btn is a cCommandLinkButton
        Set Size to 23 92
        Set Location to 187 204
        Set psImage to "CopyToBelow.ico"
        Set piImageSize to 32
        Set Label to "Copy"
        Set psToolTip to "Copy the selected Source field to the list below"
        Set peAnchors to anBottomLeft

        Procedure OnClick
            Send DoCopyItem of oParentFromIndexFields_tv
        End_Procedure

    End_Object

    Object oChildFromFields_tb is a cRDCTextbox
        Set Label to "Selected Source Fields:"
        Set Location to 219 184
        Set Size to 10 101
        Set peAnchors to anBottomLeft
    End_Object

    Object oChildFromIndexFields_tv is a cChildFieldsTreeView
        Set peAnchors to anBottomLeft
        Set Size to 72 150
        Set Location to 231 170
        Set phoParent to (oParentFromIndexFields_tv(Self))
        Set psFieldName to "FromFieldsToIdx"
        Set Status_Help to "Double-click or press the Del key to de-select a Source field."
        Set pbFrom to True
        Set Server to (Main_DD(Self))

        Property Handle phoCount (oSnctable_Toidxselcount(Self))

        Procedure DoUpdateData Integer iFromFile Integer iToFile Boolean bFrom
            Set piFile to iFromFile
            Send OnCreateTree
        End_Procedure

        Procedure UpdateDDO
            Send DoUpdateDDBuffer (Main_DD(Self)) "FromFieldsToIdx" "ToIdxSelCount"
        End_Procedure

        Procedure DoAddItem String sLabel Integer iData
            Boolean bOk
            Get IsCheckSegmentCount to bOk
            If (not(bOk)) Begin
                Procedure_Return
            End
            Forward Send DoAddItem sLabel iData
            Send UpdateDDO
        End_Procedure

        Procedure DoInsertItem String sLabel Integer iData
            Boolean bOk
            Get IsCheckSegmentCount to bOk
            If not bOk Begin
                Procedure_Return
            End
            Forward Send DoInsertItem sLabel iData
            Send UpdateDDO
        End_Procedure

        Procedure DoCutItem
            Forward Send DoCutItem
            Send UpdateDDO
        End_Procedure

        Function IsCheckSegmentCount Returns Boolean
            Integer iItems iSegments
            Get Value     of (phoCount(Self))          to iItems
            Get ItemCount of (oToIndexFields_tv(Self)) to iSegments
            If (iItems >= iSegments) Begin
                Send Info_Box "The number of fields to fill the selected index has already been reached. If you want to add another field, you first need to delete a field by pressing the 'Del' key, in this list."
            End
            Function_Return (iItems < iSegments)
        End_Function
    End_Object

    Object oToIndexFields_tb is a cRDCTextbox
        Set Label to "Target Index Fields:"
        Set Location to 219 330
        Set Size to 10 61
        Set peAnchors to anBottomLeft
    End_Object

    Object oToIndexFields_tv is a TreeView
        Set peAnchors to anBottomLeft
        Set Size to 72 150
        Set Location to 231 330
//        Set piBackColor to clLtGray
        Set Status_Help to "The segments (or fields) that the selected Target index consist of."
        Set TreeEditLabelsState to False
        Set TreeSortedState     to False
        Set pbFullRowSelect     to True
        Set TreeButtonsState    to True
        Set TreeLinesState      to True
        Set TreeRetainSelState  to True
        Set TreeRootLinesState  to True

        Procedure DoDeleteAllData
            Handle hRootItem
            Get RootItem to hRootItem
            While (hRootItem <> 0)
                Send DoDeleteItem hRootItem
                Get RootItem to hRootItem
            Loop
        End_Procedure

        Procedure OnCreateTree
            Handle hItem
            Integer iFile iIndex iFields iField iCount iOpen
            String sFieldName sSpace
            Move giToFile to iFile
            Get Field_Current_Value of (Main_DD(Self)) Field SncTable.ToIndex to iIndex
            Send DoDeleteAllData
            Get_Attribute DF_FILE_OPENED of iFile to iOpen
            If (not(iOpen)) Begin
                Procedure_Return
            End
            Get_Attribute DF_INDEX_NUMBER_SEGMENTS of iFile iIndex to iFields
            For iCount From 1 to iFields
                Move "" to sSpace
                If (iFields >= 10 and iCount < 10) Begin
                    Move " " to sSpace
                End
                Get_Attribute DF_INDEX_SEGMENT_FIELD of iFile iIndex iCount to iField
                Get FieldSpecs iFile iField                                 to sFieldName
                Get AddTreeItem (sSpace + String(iCount) * "-" * sFieldName) 0 iCount 0 0 to hItem
            Loop
        End_Procedure

    End_Object

    Object oSnctable_Toidxselcount is a cRDCDbForm
        Entry_Item Snctable.Toidxselcount
        Set Label to "Select Count"
        Set Size to 13 23
        Set Location to 308 297
        Set peAnchors to anBottomLeft
        Set Label_Col_Offset to 2
        Set Label_Justification_Mode to jMode_Right
        Set Enabled_State to False

        Procedure Set Enabled_State Boolean bState
            Forward Set Enabled_State to False
        End_Procedure
    End_Object

    Object oOK_btn is a cRDCButton
        Set Label    to "&OK"
        Set Location to 329 373
        Set peAnchors to anBottomRight

        Procedure OnClick
            Set pbOK to True
            Send Close_Panel
        End_Procedure

    End_Object

    Object oCancel_btn is a cRDCButton
        Set Size to 14 50
        Set Label    to "&Cancel"
        Set Location to 329 429
        Set peAnchors to anBottomRight

        Procedure OnClick
            Set pbOK to False
            Send Close_Panel
        End_Procedure

    End_Object

    Procedure Popup
        Handle hoDDO

        Get Main_DD to hoDDO
        Set phoChild of oParentFromIndexFields_tv to (oChildFromIndexFields_tv(Self))
        Set phoDD  of oSnctable_Toindex         to hoDDO
        Set Server of oSnctable_Toindex         to hoDDO
        Set Server of oParentFromIndexFields_tv to hoDDO
        Set Server of oChildFromIndexFields_tv  to hoDDO
        Send DoComboFillList of oSnctable_Toindex
        Send DoCreateTree of oParentFromIndexFields_tv
        Send DoCreateTree of oChildFromIndexFields_tv

        Forward Send Popup
    End_Procedure

    Object oInfoChild_tb is a cRDCTextbox
        Set Label to "Each of the Target fields from the treeview to the right, needs to be matched with a Source field from the treeview above."
        Set Auto_Size_State to False
        Set Location to 238 78
        Set Size to 55 80
        Set Justification_Mode to jMode_Left
        Set peAnchors to anBottomLeft
        Set FontWeight to fw_Bold
    End_Object

    Object oDeleteFrom_btn is a cRDCButton
        Set Size to 14 73
        Set Location to 308 170
        Set Label to "Remove Source Field"
        Set psToolTip to "Remove the selected Source field"
        Set peAnchors to anBottomLeft

        Procedure OnClick
            Send DoCutItem of oChildFromIndexFields_tv
        End_Procedure

    End_Object

    On_Key Key_Alt+Key_O Send KeyAction of oOk_btn
    On_Key Key_Alt+Key_C Send KeyAction of oCancel_btn
End_Object

Function Edit_oSelectIndexAndSegments Handle hoDDO Returns Boolean
    Handle ho
    Boolean bOK

    Move (oSelectIndexAndSegments(Self)) to ho
    Set Main_DD of ho to hoDDO
    Set Server  of ho to hoDDO
    Send Popup of ho
    Get pbOK of ho to bOK

    Function_Return bOK
End_Function

