//****************************************************************************
// $Module type: CLASS
// $Module name: DoInitValue
// $Author     : Nils G. Svedmyr
// Created     : 2002-07-21 @ 09:30
//
// Description :
//
// $Rev History
//    2002-07-21  Module header created
//****************************************************************************
Declare_Datafile SncTable
Use cRDCDbForm.pkg

Class cDbFormPassword is a cRDCDbForm
    Procedure Construct_Object
        Forward Send Construct_Object
        Property Integer piDbType EN_DbTypeDataFlex
        Property Boolean pbFrom   False
        Property Handle  phoDD    (Main_DD(Self))
        Property String Private.StatusHelp ""     // Do _not_ use Private.Status_Help. It is defined in base class.
        Set Password_State to True
        Set Entry_msg to DoCheckEntering
    End_Procedure

    Procedure End_Construct_Object
        Forward Send End_Construct_Object
    End_Procedure

    Procedure DoCheckEntering
        // If Not (Enabled_State(Self)) Send Next
    End_Procedure

    Procedure DoEnableDisable Integer iValue
        Set piDbType to iValue
        Set Enabled_State to (iValue > EN_DbTypeDataFlex)
        Send DoClearDisabled
    End_Procedure

    Procedure DoClearDisabled
        Handle hoDD
        Integer iFile iField iType
        Boolean bChanged bNTAuth
        String sHelp

        Get phoDD to hoDD
        If (pbFrom(Self)) Begin
            Get Field_Current_Value of hoDD Field SncTable.FromNTAuth to bNTAuth
        End
        Else Begin
            Get Field_Current_Value of hoDD Field SncTable.ToNTAuth   to bNTAuth
        End
        Get piDbType to iType

        Get Private.StatusHelp to sHelp
        If (sHelp = "") Begin
            Get Status_Help Item 0 to sHelp
            Set Private.StatusHelp to sHelp
        End

        Get phoDD      to hoDD
        Get Data_File  to iFile
        Get Data_Field to iField
        If (not(Enabled_State(Self))) Begin
            Get File_Field_Changed_State     of hoDD iFile iField to bChanged
            If bChanged Begin
                Set File_Field_Changed_Value of hoDD iFile iField to ""
            End
        End
        If (iType < EN_DbTypePervasive) Begin
            Get Private.StatusHelp to sHelp
            Set Status_Help        to sHelp
            If (iType = EN_DbTypeMSSQL and bNTAuth = True) Begin
                Set Enabled_State to False
            End
        End
        Else If (iType = EN_DbTypePervasive) Begin
            Set Status_Help to ;
            ("Pervasive.SQL (Btrieve) has a concept called the ownername of a file." * ;
            "The value of an ownername is not the name as the person who created the file" * ;
            "or the person who owns it. You should think of ownernames like passwords."    * ;
            "People who know the password have unlimited access, while people who don't, have not." * ;
            "NOTE: Ownernames are case sensitive!")
        End

    End_Procedure

End_Class
