//****************************************************************************
// $Module type: Class
// $Module name: cdbCheckboxNtAuth.pkg
// $Author     : Nils Svedmyr
// Created     : 2002-09-25 @ 08:26
//
// Description :
//
// $Rev History
//    2002-09-25  Module header created
//****************************************************************************
Use cRDSDataDictionary.pkg
Use SyncFuncs.pkg

Class cDbCheckboxNtAuth is a dbCheckbox
    Procedure Construct_Object
        Forward Send Construct_Object
        Property Handle phoUserObject     0
        Property Handle phoPasswordObject 0
        Property Handle phoDD (Main_DD(Self))
        Property Integer piDbType EN_DbTypeDataFlex
        { InitialValue=True }
        Property Boolean pbFrom True
    End_Procedure

    Procedure End_Construct_Object
        Forward Send End_Construct_Object
    End_Procedure

    Procedure DoEnableDisable Integer iValue
        Handle hoDD
        Get phoDD    to hoDD
        Set piDbType to iValue
        Set Visible_State to True
        If (iValue = 4) Begin
            Set Visible_State to False
        End
        Set Enabled_State to (iValue = EN_DbTypeMSSQL or iValue = EN_DbTypeOracle)
        If (Enabled_State(Self) = False) Begin
            Set Checked_State to False
        End
        If (pbGetHasCurrRowId(hoDD) = False and iValue = 3) Begin
            Set Checked_State to True
        End
    End_Procedure

    Procedure OnChange
        Handle hoDD ho
        Boolean bSelect
        Integer iFile iField iFromState iToState iShould_Save iChanged_State

        If (Active_State(Self) = False or Enabled_State(Self) = False) Begin
            Procedure_Return
        End

        Get Main_DD to hoDD
        Get Select_State to bSelect
        If (phoUserObject(Self) = False or phoPasswordObject(Self) = False) Begin  // Only do this once.
            If (pbFrom(Self)) Begin
                Get DDObjectId hoDD SncTable.File_Number "FromUser" to ho // Global function in Sysfuncs.pkg
                Set phoUserObject                                   to ho
                Get DDObjectId hoDD SncTable.File_Number "FromPv"   to ho
                Set phoPasswordObject                               to ho
            End
            Else Begin
                Get DDObjectId hoDD SncTable.File_Number "ToUser"   to ho
                Set phoUserObject                                   to ho
                Get DDObjectId hoDD SncTable.File_Number "ToPv"     to ho
                Set phoPasswordObject                               to ho
            End
        End

        Set Enabled_State of (phoUserObject(Self))         to (bSelect = False)
        Set Enabled_State of (phoPasswordObject(Self))     to (bSelect = False)
        If (bSelect = False) Begin
            Procedure_Return
        End

        Get Should_Save   of hoDD                          to iShould_Save
        Get Changed_State of hoDD                          to iChanged_State
        Get Data_File     of (phoUserObject(Self))         to iFile
        Get Data_Field    of (phoUserObject(Self))         to iField
        Get File_Field_Changed_State of hoDD iFile iField  to iFromState
        Set File_Field_Changed_Value of hoDD iFile iField  to ""
        Set File_Field_Changed_State of hoDD iFile iField  to iFromState
        Get Data_File  of (phoPasswordObject(Self))        to iFile
        Get Data_Field of (phoPasswordObject(Self))        to iField
        Get File_Field_Changed_State of hoDD iFile iField  to iToState
        Set File_Field_Changed_Value of hoDD iFile iField  to ""
        Set File_Field_Changed_State of hoDD iFile iField  to iToState
        Set Changed_State of hoDD                          to iChanged_State
    End_Procedure

End_Class
