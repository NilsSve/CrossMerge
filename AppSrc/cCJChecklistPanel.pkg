Use cApplication.pkg
Use cCJCommandBars.pkg

// Note: The ghoChecklistPanel object handle can only be used if there's only *one*
//       object isntantiated of this class.
Global_Variable Handle ghoChecklistPanel
{ DesignerClass=cDTToolPanel }
//{ OverrideProcedureSet=Border_Style InitialValue='Border_None' }
{ OverrideProperty=Visible_State DesignTime=False }
Class cCJCheckListPanel is a Container3d

    Procedure Construct_Object
        Forward Send Construct_Object

        Move Self to ghoChecklistPanel

        { MethodType=Property }
        Property String  psLabel ''

        { MethodType=Property InitialValue=xtpBarFloating }
        { EnumList='xtpBarTop,xtpBarBottom,xtpBarLeft,xtpBarRight,xtpBarFloating' }
        Property Integer peBarPosition xtpBarRight

        { DesignTime=False }
        // cCJDialogBar object
        Property Handle phoDialogBar

        Property Boolean pbResizable True

        Set Border_Style to Border_None
        Set Color to clInfoBk
        Set peAnchors to anAll
    End_Procedure

    Procedure Set ComVisible Boolean bState
        Set Visible_State to bState
        Send Show_Panel bState
    End_Procedure

    Function ComVisible Returns Boolean
        Boolean bState
        Get Visible_State to bState
        Function_Return bState
    End_Function

    Procedure Show_Panel Boolean bState
        Handle hoDialogBar
        Boolean bActive
        Get phoDialogBar to hoDialogBar
        If (hoDialogBar = 0) Begin
            Procedure_Return
        End
        Move (IsComObjectCreated(hoDialogBar)) to bActive
        If (hoDialogBar and bActive = True) Begin
            Set ComVisible of hoDialogBar to bState
        End
    End_Procedure

    Procedure ToggleChecklistPanel
        Boolean bVisible
        If (phoDialogBar(Self)) Begin
            Get ComVisible to bVisible
            Send WriteDWord of ghoApplication "Settings" "Checklists" (not(bVisible))
            //            If (bVisible = False) Begin
            //                Get Size to iSize
            //                Send WriteDWord of ghoApplication "Settings" "CheckListWith" (Low(iSize))
            //            End
            Set ComVisible to (not(bVisible))
        End
    End_Procedure

    Procedure Close_Panel
        Send Show_Panel False
    End_Procedure

    Procedure Activate Returns Integer
        Integer iErr
        Handle hoScopeObj hoMain

        Get Current_Scope to hoScopeObj
        If (not(Active_State(Self)) and (hoScopeObj > Desktop)) Begin
            If (ghoApplication) Begin
                Move (Client_Id(phoMainPanel(ghoApplication))) to hoMain
                Send Activate of hoMain
            End
        End

        Forward Get msg_Activate to iErr
        If (not(iErr)) Begin
            Send Show_Panel True
        End
        Procedure_Return iErr
    End_Procedure

    {MethodType=Event}
    Procedure OnCreateChecklistPanel
    End_Procedure

    Procedure CreateChecklistPanel
        Variant vChecklistPane
        String sLabel sObjName
        Boolean bResizable
        Integer eBarPosition iSize iWidth iHeight
        Handle hoChecklistPane hWnd

        If (ghoCommandBars) Begin
            Get psLabel to sLabel
            Get Object_Label to sObjName

            Get Container_Handle to hWnd
            Get pbResizable to bResizable

            Move (sObjName + "Pane") to sObjName

            Get piMinSize to iSize
            If (iSize = 0) Begin
                Get Size to iSize
            End

            Get DialogToGui (Hi(iSize)) (Low(iSize)) to iSize
            Move (Hi(iSize))  to iHeight
            Move (Low(iSize)) to iWidth

            Get peBarPosition to eBarPosition

            Get ComAddDialogBar of ghoCommandBars sLabel eBarPosition to vChecklistPane
            Get CreateNamed     of ghoCommandBars (RefClass(cCJDialogBar)) sObjName to hoChecklistPane
            If (hoChecklistPane) Begin
                Set pvComObject     of hoChecklistPane to vChecklistPane
                Set ComTitle        of hoChecklistPane to sLabel
                Set ComChildHandle  of hoChecklistPane to hWnd
                Set ComVisible      of hoChecklistPane to False
                Set ComShowGripper  of hoChecklistPane to True
                Set ComResizable    of hoChecklistPane to bResizable
                Send ComSetSize     of hoChecklistPane iWidth iHeight
            End

            Set phoDialogBar to hoChecklistPane
            Send OnCreateChecklistPanel
        End

    End_Procedure

    Procedure Add_Focus Handle hoParent Returns Integer
        Integer iErr

        Forward Get msg_Add_Focus hoParent to iErr

        If (not(iErr)) Begin
            If (ghoCommandBars) Begin
                Send AddActiveAction of ghoCommandBars Self
            End
            Else Begin
                Send Update
            End
            Send OnResize
        End
        Procedure_Return iErr
    End_Procedure

    Procedure OnUpdate
    End_Procedure

    Procedure Update
        If (ghoCommandBars) Begin
            Send RemoveActiveAction of ghoCommandBars Self
        End
        Send CreateChecklistPanel
        Send Show_Panel (Visible_State(Self))
        Send OnUpdate
    End_Procedure

    Function DEO_Object Returns Boolean
        Function_Return False
    End_Function

    Function ToolPanel_Handle Returns Handle
        Function_Return (Self)
    End_Function

    Procedure End_Construct_Object
        String sLabel
        Integer iBorder

        Forward Send End_Construct_Object

        Set Locate_Mode to SMART_LOCATE
        Get Label to sLabel
        Get Border_Style to iBorder
        Set Border_Style  to Border_None
        Set Visible_State to False  // same as default ComVisible
        Set Label to ""
        If (psLabel(Self) = "") Begin
            Set psLabel to sLabel
        End
    End_Procedure

End_Class
