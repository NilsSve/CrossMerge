Use Windows.pkg
Use Dfbitmap.pkg
Use cRDCDbModalPanel.pkg

Define C_DPV For "robin92"

Object oDeletePassword_dg is a cRDCDbModalPanel
    Set Size to 62 308
    Set Label to "CrossMerge Builder"
    Set Sysmenu_Icon to False
    Set Minimize_Icon to False
    Set Border_Style to Border_Normal

    Property Integer piPasswordOk -1

    Object BitmapContainer1 is a BitmapContainer
        Set Size to 20 25
        Set Location to 4 15
        Set Bitmap to "Keys32.bmp/T/3D"
        Set Border_Style to Border_None
        Set Dialog_Caption to ""
    End_Object    

    Object Textbox1 is a Textbox
        Set Label to "Enter password to be able to delete records:"
        Set FontSize to 4 0
        Set Location to 5 61
        Set Size to 10 140
        //    Set TypeFace to "MS Sans Serif"
    End_Object    

    Object oPassword_fm is a Form
        Set Label to "&Password:"
        Set Size to 13 90
        Set Location to 23 106
        Set Label_Col_Offset to 45
        Set Password_State Item 0 to True
    End_Object    

    Object oCancel_bn is a Button
        Set Label to "&Cancel"
        Set Size to 14 50
        Set Location to 39 244
        Set Status_Help to "Close the dialog"
        Set Default_State to TRUE
        Set Bitmap to "Cancel16.bmp"
        //    Set piTransparentColor to clFuchsia

        Procedure OnClick
            Delegate Set piPasswordOk to -1
            Send Close_Panel
        End_Procedure 
    End_Object    

    Object oOK_bn is a Button
        Set Label to "OK"
        Set Size to 14 50
        Set Location to 23 244
        Set Status_Help to "Accept password and start deleting records"
        Set Bitmap to "Ok16.bmp"
        //    Set piTransparentColor to clFuchsia

        Procedure OnClick
            String sPassword sText
            Get Value of oPassword_fm to sPassword
            Move (Trim(sPassword))    to sPassword
            If (sPassword <> C_DPV) Begin
                Move ("The password is incorrect. Please retype your password. " + ;
                "Letters in passwords must be typed using the correct case. Make sure that Caps Lock is not accidently on.") to sText
                Send Stop_Box sText
                Set Value of oPassword_fm to ""
                Delegate Set piPasswordOk to 0
                Procedure_Return
            End
            Else Begin
                Delegate Set piPasswordOk to 1
                Send Close_Panel
            End 
        End_Procedure 
    End_Object    

    On_Key Key_Ctrl+Key_F4 Send OnClick  to oCancel_bn // Aborts
    On_Key Key_Alt+Key_F4  Send OnClick  to oCancel_bn
    On_Key Key_Alt+Key_C   Send OnClick  to oCancel_bn
    On_Key kCancel         Send OnClick  to oCancel_bn
    On_Key Key_Alt+Key_P   Send Activate to oPassword_fm
End_Object    

Function IsDeletePasswordOk Returns Integer
    Handle ho
    Move (oDeletePassword_dg(Self)) to ho
    Set Value of (oPassword_fm(ho)) to ""
    Set piPasswordOk of ho          to -1
    Send Popup of ho
    Function_Return (piPasswordOk(ho))
End_Function 
