//****************************************************************************
// $Module type: Class
// $Module name: cChildFieldsTreeView.pkg
// $Author     : Nils Svedmyr
// Created     : 2001-08-17 @ 15:07
//
// Description :
//
// $Rev History
//    2001-08-17  Module header created
//****************************************************************************
Use cRDSTreeView.pkg
Use cRDSDataDictionary.pkg
Use Working.pkg
Use SyncFuncs.pkg

Open SncTable

{ OverrideProperty=Border_Style InitialValue=Border_ClientEdge }
{ OverrideProperty=Border_Style EnumList="Border_None, Border_Normal, Border_Dialog, Border_Thick, Border_ClientEdge, Border_WindowEdge, Border_StaticEdge" }
Class cChildFieldsTreeView is a cRdsTreeView
    Procedure Construct_Object
        Forward Send Construct_Object

        Property Handle phoParent 0
        { InitialValue=0 }
        Property Integer Server   0
        Property Integer piFile   0
        Property String  psFieldName ""
        { InitialValue=True }
        Property Boolean pbFrom   False

        Set pbFullRowSelect     to True
        Set pbHotTracking       to False
        Set pbShowTooltips      to True
        Set pbSingleExpand      to False
        Set TreeButtonsState    to False
        Set TreeEditLabelsState to False
        Set TreeLinesState      to True
        Set TreeRetainSelState  to True
        Set TreeRootLinesState  to True
        Set TreeSortedState     to False
        Set Oem_Translate_State to False
        Set TreeLinesState      to True

        Send Ignore_Error of Error_Info_Object 4100 // "Cannot find field". to not trigger error while trying to find a matching field name in ToFile.

        On_Key kDelete_Character Send DoCutItem
        On_Key Key_Ctrl+Key_X    Send DoCutItem
        On_Key kEnter            Send DoCutItem
        On_Key Key_F6            Send Switch
        On_Key Key_Shift+Key_F6  Send Switch_Back
        On_Key kSave_Record      Send Request_Save of (Main_DD(Self))
        On_Key Key_Ctrl+Key_S    Send Request_Save of (Main_DD(Self))
    End_Procedure

    Procedure End_Construct_Object
        Forward Send End_Construct_Object
    End_Procedure

    Procedure DoUpdateData Integer iFromFile Integer iToFile Boolean bFrom
        Boolean bFromLocal

        Get pbFrom to bFromLocal
        If (bFrom <> bFromLocal) Begin
            Procedure_Return
        End
        If (pbFrom(Self)) Begin
            Set piFile to iFromFile
        End
        Else Begin
            Set piFile to iToFile
        End
        Send OnCreateTree
    End_Procedure

    Procedure OnCreateTree
        Handle ho
        // We don't want parent TreeView to fill until this object has been created:
        Get phoParent to ho
        If ho Begin
            Send DoCreateTree of ho
        End
        Send DoCreateTree
    End_Procedure

    Procedure DoCreateTree
        Handle hItem ho
        Boolean bAppend bTableChanged
        Integer iRDCFile iRDCField iFile iField iSrv iPos iCount iOpen
        String sFieldName sSelFields sSpace sLabel sConcat

        Set Dynamic_Update_State to False
        Send DoDeleteAllData
        Move SncTable.File_Number to iRDCFile
        Get piFile to iFile // The file to act upon.
        Get psFieldName to sFieldName
        If (not(iFile) or sFieldName = "") Begin
            Set Dynamic_Update_State to True
            Procedure_Return
        End
        Field_Map iRDCFile sFieldName to iRDCField
        Get Server                    to iSrv
        If (iSrv = 0) Begin
            Procedure_Return
        End
        If (iRDCField and iSrv) Begin
            Get File_Field_Current_Value of iSrv iRDCFile iRDCField to sSelFields
        End
        Move (Trim(sSelFields)) to sSelFields
        If iFile Begin
            Get_Attribute DF_FILE_OPENED of iFile to iOpen
        End
        If (sSelFields = "" or not(iOpen)) Begin
            Set Dynamic_Update_State to True
            Procedure_Return
        End
        Move (Self) to ho

        // This will take care of a special situation.
        // The user has created a record and has also specified Selfields, he then changes the
        // datatable comboform to another table...
        If (pbGetHasCurrRowId(iSrv)) Begin
            If (pbFrom(Self)) Begin
                Get Field_Changed_State of iSrv Field SncTable.FromDataTable to bTableChanged
            End
            Else Begin
                Get Field_Changed_State of iSrv Field SncTable.ToDataTable   to bTableChanged
            End
            If bTableChanged Begin
                // Reset DDO buffer value in case user saves the new config.
                Set File_Field_Changed_Value of iSrv iRDCFile iRDCField to ""
                Set Dynamic_Update_State to True
                Procedure_Return
            End
        End

        Send Ignore_Error of Error_Info_Object 77
        Move False to Err

        Repeat
            Move (Pos(' ', sSelFields)) to iPos
            If iPos Begin
                Move (Left(sSelFields, (iPos - 1))) to iField
                Move (Trim(Replace((String(iField) + ' '), sSelFields, ''))) to sSelFields
            End
            Else Begin
                Move (Trim(sSelFields)) to iField // Last item
                Move ""                 to sSelFields
            End
            Move (iField < 0) to bAppend
            If bAppend Begin
                Move (Abs(iField)) to iField
            End
            Move "" to sSpace     // We need to use spaces for field numbers to get it display correctly.
            If (iField >=  0 and iField < 10) Begin
                Move "  " to sSpace
            End
            Else If (iField >= 10 and iField < 100) Begin
                Move " "  to sSpace
            End

            Get FieldSpecs iFile iField to sFieldName // Function in SyncFuncs.pkg
            Increment iCount
            If bAppend Begin
                If (sConcat = "") Begin
                    Get ItemLabel hItem to sLabel
                    Move (String(sLabel)     * "+" * String(sSpace) + String(iField) * "-" * sFieldName) to sConcat
                End
                Else Begin
                    Move (String(sConcat) * "+" * String(sSpace) + String(iField) * "-" * sFieldName) to sConcat
                End
            End
            Else Begin
                If (sConcat <> "") Begin
                    Set ItemLabel hItem to sConcat
                    Move "" to sConcat
                End
                Get AddTreeItem (sSpace + String(iField) * "-" * sFieldName) 0 iField 0 0 to hItem
            End
            If (sConcat <> "") Begin // If appended field on last selection field.
                Set ItemLabel hItem to sConcat
                Move "" to sConcat
            End
            If (Err) Break
        Until (Trim(sSelFields) = "")

        Send Trap_Error of Error_Info_Object 77
        Set Dynamic_Update_State to True
        Send Paint
    End_Procedure

    Procedure DoAddItem String sLabel Integer iData
        Handle hItem
        Get AddTreeItem sLabel 0 iData 0 0 to hItem
    End_Procedure

    Procedure OnItemDblClick Handle hItem
        Send DoCutItem
    End_Procedure

    Procedure DoCutItem
        Handle hItem
        Integer iData
        String sLabel

        If (phoParent(Self) = False) Begin
            Procedure_Return
        End
        Get CurrentTreeItem to hItem
        If (hItem = 0) Begin
            Procedure_Return
        End
        Get ItemLabel hItem to sLabel
        Get ItemData hItem  to iData
        Send DoDeleteItem hItem
    End_Procedure

    Procedure DoInsertItem String sLabel Integer iData
        Handle hItem
        If (phoParent(Self) = 0) Begin
            Procedure_Return
        End
        Get CurrentTreeItem to hItem
        If (hItem = 0) Begin
            Get RootItem to hItem
            Set CurrentTreeItem to hItem
        End
        Get PreviousSiblingItem hItem to hItem
        If (hItem = 0) Begin
            Get RootItem to hItem
        End
        Get InsertTreeItem sLabel 0 hItem iData 0 0 to hItem
        Set CurrentTreeItem to hItem // to be or not to be...
    End_Procedure

    Function ItemNumber Handle hItem Returns Integer
        Integer iItems iCount iData iDataNew
        Handle hNext

        Get ItemCount to iItems
        Decrement iItems
        Get ItemData hItem to iData
        Get RootItem to hNext
        For iCount From 0 to iItems
            Get ItemData hNext to iDataNew
            If (iData = iDataNew) Break
            Get NextSiblingItem hNext to hNext
        Loop
        Function_Return iCount
    End_Function

    Function ItemHandle Integer iItem Returns Handle
        Integer iCount
        Handle hItem

        Get RootItem to hItem
        For iCount From 0 to (iItem -1)
            Get NextSiblingItem hItem to hItem
        Loop
        Function_Return hItem
    End_Function

    Procedure DoUpdateDDBuffer Integer iSrv String sFieldName String sFieldCountField
        Handle hItem
        Integer iField iFile iCount iOldVal
        String sNewVal sOldVal sLabel sData

        Move SncTable.File_Number   to iFile
        Field_Map iFile sFieldName  to iField
        Get RootItem                to hItem
        Repeat
            Get ItemLabel hItem to sLabel
            If (sLabel contains "+") Begin
                Get MultiFields sLabel to sData
            End
            Else Begin
                Get ItemData   hItem to sData
            End
            If (sData <> "") Begin
                Move (String(sNewVal) * String(sData)) to sNewVal
            End
            Get NextSiblingItem hItem to hItem
        Until (hItem < 1)

        Get ItemCount to iCount
        If (iCount = 0) Begin
            Move " "    to sNewVal // Note: Must be a space, else change is not saved.
        End
        Else Begin
            Move (Trim(sNewVal)) to sNewVal
        End
        If (iField <> 0) Begin
            Get File_Field_Current_Value   of iSrv iFile iField to sOldVal
            If (Trim(sOldVal <> sNewVal)) Begin
                Set File_Field_Changed_Value of iSrv iFile iField to sNewVal
            End
        End
        If (sFieldCountField <> "") Begin
            Field_Map iFile sFieldCountField to iField
            If (iField <> 0) Begin
                Get File_Field_Current_Value of iSrv iFile iField to iOldVal
                If (iOldVal <> iCount) Begin
                    Set File_Field_Changed_Value of iSrv iFile iField to iCount
                End
            End
        End
    End_Procedure

    // Used when fields have been appended together,
    // aka more than one field in a row.
    Function MultiFields String sLabel Returns String
        Integer iPos
        String sData sField sTmp

        Move (Pos("-", sLabel)) to iPos
        Move (Left(sLabel, (iPos - 2))) to sField
        Move sField to sData
        Move (Pos("+", sLabel))   to iPos
        Move (Left(sLabel, iPos)) to sTmp
        Move (Replace(sTmp, sLabel, "")) to sLabel
        Move (Trim(sLabel) * "+") to sLabel

        Repeat
            Move (Pos("+", sLabel)) to iPos
            If (iPos <> 0) Begin
                Move (Left(sLabel, iPos))           to sTmp
                Move (Pos("-", sTmp))               to iPos
                Move (Left(sTmp, (iPos - 2)))       to sField
                Move ("-" + Trim(sField))           to sField
                Move (String(sData) * Trim(sField)) to sData
                Move (Replace(sTmp, sLabel, ""))    to sLabel
            End
        Until (not(sLabel contains "+") or not(iPos))

        Function_Return sData
    End_Function

    Procedure DoDeleteAllData
        Handle hRootItem
        Get RootItem to hRootItem
        While (hRootItem <> 0)
            Send DoDeleteItem hRootItem
            Get RootItem to hRootItem
        Loop
    End_Procedure

End_Class
