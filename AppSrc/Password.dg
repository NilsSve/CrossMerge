Use SyncFuncs.pkg
Use cRDCDbModalPanel.pkg

Use SncSys.dd

Object oPassword_dg is a cRDCDbModalPanel
    Set Locate_Mode to Center_On_Parent
    Set Border_Style to Border_Normal
    Set Sysmenu_Icon to False
    Set Minimize_Icon to False
    Set Label to "Welcome to CrossMerge Builder"
    Set Size to 92 223
    Set Icon to "CmBuilder.ico"
    
    Object oSncsys_DD is a Sncsys_DataDictionary  
        Set Auto_Fill_State to True
    End_Object

    Object oInfo_tb is a Textbox
        Set Label to "Enter user name and password to login:"
        Set Location to 5 42
        Set Size to 10 125
    End_Object

    Object oPassword_fm is a Form
        Set Label to "&Password:"
        Set Size to 13 90
        Set Location to 40 87
        Set Label_Col_Offset to 45
        Set Password_State Item 0 to True
    End_Object

    Object oName_fm is a Form
        Set Label to "&Name:"
        Set Size to 13 90
        Set Location to 24 87
        Set Label_Col_Offset to 45

        Procedure OnStartup
            String sName
            Get Field_Current_Value of oSncsys_DD Field SncSys.User to sName
            If (sName = "") Begin
                Get Network_User_Name to sName
            End
            Set Value to sName
        End_Procedure
        Send OnStartUp

    End_Object

    Object oOK_bn is a Button
        Set Label to "OK"
        Set Size to 14 50
        Set Location to 72 113
        Set Status_Help to "Login to CrossMerge Builder"
        Set Default_State to True

        Procedure OnClick
            String sName sPassword sOrgName sOrgPv sText

            Move SncSys.User          to sOrgName
            Move (Trim(sOrgName))     to sOrgName
            Move (Trim(SncSys.Pv))    to sOrgPv
            Get Value of oName_fm     to sName
            Move (Trim(sName))        to sName
            Get Value of oPassword_fm to sPassword
            Move (Trim(sPassword))    to sPassword
            Move ("The user name or password is incorrect. Please retype your password and/or user name. " + ;
            "Letters in passwords must be typed using the correct case. Make sure that Caps Lock is not accidently on.") to sText

            // Note: Checking for User name in UPPERCASE:
            If (Uppercase(sOrgName) <> Uppercase(sName) or sOrgPv <> sPassword) Begin
                Send Stop_Box sText
                Set Value of oPassword_fm to ""
                Procedure_Return
            End
            Else Begin
                Send Close_Panel
            End
        End_Procedure

    End_Object

    Object oCancel_bn is a Button
        Set Label to "&Cancel"
        Set Size to 14 50
        Set Location to 72 166
        Set Status_Help to "Close this dialog and the program"

        Procedure OnClick
            Send Exit_Application
        End_Procedure

    End_Object

    On_Key Key_Ctrl+Key_F4 Send OnClick  to oCancel_bn
    On_Key Key_Alt+Key_F4  Send OnClick  to oCancel_bn
    On_Key Key_Alt+Key_N   Send Activate to oName_fm
    On_Key Key_Alt+Key_P   Send Activate to oPassword_fm
End_Object
