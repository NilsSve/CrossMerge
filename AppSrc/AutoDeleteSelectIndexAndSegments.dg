Use cRDCDbModalPanel.pkg
Use cRDCTextbox.pkg
Use cRDCDbForm.pkg
Use cDbIndexComboForm.pkg
Use cViewDataButton.pkg
Use cParentFieldsTreeview.pkg
Use cChildFieldsTreeView.pkg

Object oAutoDeleteSelectIndexAndSegments is a cRDCDbModalPanel
    Set Label to "Auto Delete Old Records - Select Index And Segments"
    Set Size to 221 485
    Set piMinSize to 221 485
    Set Location to 2 2

    Property Handle Main_DD
    Property Handle Server
    Property Boolean pbOK False

    Object oInfo_tb is a cRDCTextbox
        Set Auto_Size_State to False
        Set Size to 21 472
        Set Location to 4 12
        Set Label to "Note: This setup is *only* necessary when using the 'Auto Delete Old Records' logic."
        Set FontPointHeight to 12
        Set Justification_Mode to JMode_Center
    End_Object

    Object oSnctable_FromdataTable_Index is a cRDCDbForm
        Entry_Item Snctable.Fromdatatable
        Set Label to "Table Name"
        Set Size to 13 150
        Set Location to 40 12
        Set Label_Col_Offset to 0
        Set Label_Justification_Mode to JMode_Top
        Set Enabled_State to False
        Set Label_Row_Offset to 1

        Procedure Set Enabled_State Boolean bState
            Forward Set Enabled_State to False
        End_Procedure
    End_Object

    Object oSnctable_Fromindex is a cDbIndexComboform
        Entry_Item Snctable.Fromindex
        Set Label to "Select Unique Index"
        Set Size to 13 150
        Set Location to 40 170
        Set Label_Row_Offset to 1
        Set Label_Col_Offset to 0
        Set Label_Justification_Mode to JMode_Top
//        Set Entry_State to False
//        Set Combo_Sort_State to True
        Set Code_Display_Mode to cb_Code_Display_Both
        Set pbFrom to True

        Procedure OnChange
            Forward Send OnChange
            Send DoCreateTree of oChildIndexSegments
            Send OnCreateTree of oFromIndexFields_tv
        End_Procedure

    End_Object

    Object oViewFromData_bn is a cViewDataButton
        Set Label to "&View Data"
        Set Size to 13 60
        Set Location to 40 327
        Set Status_Help to "View data for the Source database table."
        Set Bitmap to "ViewData16.bmp"
        Set pbFrom to True

        Procedure OnClick
            Send DoViewData True -1
        End_Procedure
    End_Object

    Object oParentToFields_tb is a cRDCTextbox
        Set Label to "Available Target Field Numbers:"
        Set Location to 59 188
        Set Size to 10 101
    End_Object

    Object oParentIndexSegments is a cParentFieldsTreeView
        Set pbFrom to False
        Set peAnchors to anTopLeftRight
        Set Size to 54 150
        Set Location to 71 170
        Set Status_Help to "Double-click or press space-bar or the Ins key to select a Target field"
        Set phoChild to (oChildIndexSegments(Self))

        Procedure DoUpdateData Integer iFromFile Integer iToFile Boolean bFrom
            Set piFile to iToFile
        End_Procedure
    End_Object

    Object oInfo_tb is a cRDCTextbox
        Set Label to "Select the fields that should be used to populate the Source table's index, to do a find equal search. Only used when 'Auto delete old records' is checked."
        Set Auto_Size_State to False
        Set Location to 71 12
        Set Size to 54 150
        Set Justification_Mode to jMode_Left
        Set peAnchors to anTopRight
    End_Object

    Object oChildToFields_tb is a cRDCTextbox
        Set Label to "Selected Target Field Numbers:"
        Set Location to 139 178
        Set Size to 10 101
    End_Object

    Object oChildIndexSegments is a cChildFieldsTreeView
        Set peAnchors to anBottomLeftRight
        Set Size to 54 150
        Set Location to 149 170
        Set phoChild  of oParentIndexSegments to (Self)
        Set phoParent to (oParentIndexSegments(Self))
        Set psFieldName to "ToFieldsFromIdx"
        Set Status_Help to "Double-click or press the Del key to de-select a Target field."
        Set pbFrom to False
        Set Server to (Main_DD(Self))

//        Property Handle phoCount (oSnctable_Fridxselcount(Self))

        Procedure DoUpdateData Integer iFromFile Integer iToFile Boolean bFrom
            Set piFile to iFromFile
            Send OnCreateTree
        End_Procedure

        Procedure UpdateDDO
            Send DoUpdateDDBuffer (Main_DD(Self)) "ToFieldsFromIdx" "FrIdxSelCount" // Class procedure.
        End_Procedure

        Procedure DoAddItem String sLabel Integer iData
            Forward Send DoAddItem sLabel iData
            Send UpdateDDO
        End_Procedure

        Procedure DoInsertItem String sLabel Integer iData
            Forward Send DoInsertItem sLabel iData
            Send UpdateDDO
        End_Procedure

        Procedure DoCutItem
            Forward Send DoCutItem
            Send UpdateDDO
        End_Procedure
    End_Object

    Object oFromIndexFields_tb is a cRDCTextbox
        Set Label to "Source Index Segments to Match:"
        Set Location to 139 328
        Set Size to 10 107
        Set peAnchors to anTopRight
    End_Object

    Object oFromIndexFields_tv is a TreeView
        Set peAnchors to anBottomRight
        Set Size to 54 140
        Set Location to 149 325
        Set piBackColor to clLtGray
        Set TreeRootLinesState to False
        Set TreeLinesState to False
        Set TreeButtonsState to False
        Set Status_Help to "The segments (or fields) that the selected Source index consist of."

        Procedure DoDeleteAllData
            Handle hRootItem
            Get RootItem to hRootItem
            While (hRootItem <> 0)
                Send DoDeleteItem hRootItem
                Get RootItem to hRootItem
            Loop
        End_Procedure

        Procedure OnCreateTree
            Handle hItem
            Integer iFile iIndex iFields iField iCount iOpen
            String sFieldName sSpace
            Get Field_Current_Value of (Main_DD(Self)) Field SncTable.FromFile  to iFile
            Get Field_Current_Value of (Main_DD(Self)) Field SncTable.FromIndex to iIndex
            Send DoDeleteAllData
            If iFile Begin
                Get_Attribute DF_FILE_OPENED of iFile to iOpen
            End
            If not iOpen Begin
                Procedure_Return
            End
            Get_Attribute DF_INDEX_NUMBER_SEGMENTS of iFile iIndex to iFields
            For iCount From 1 to iFields
                Move "" to sSpace
                If (iFields >= 10 and iCount < 10) Begin
                    Move " "                 to sSpace
                End
                Get_Attribute DF_INDEX_SEGMENT_FIELD of iFile iIndex iCount to iField
                Get FieldSpecs iFile iField                                 to sFieldName // Function in SyncFuncs.pkg
                Get AddTreeItem (sSpace + String(iCount) * "-" * sFieldName) 0 iCount 0 0 to hItem
            Loop
        End_Procedure
    End_Object

    Object oOK_btn is a cRDCButton
        Set Label    to "&OK"
        Set Location to 204 356
        Set peAnchors to anBottomRight

        Procedure OnClick
            Send Close_Panel
        End_Procedure

    End_Object

    Object oCancel_btn is a cRDCButton
        Set Size to 14 50
        Set Label    to "&Cancel"
        Set Location to 204 411
        Set peAnchors to anBottomRight

        Procedure OnClick
            Send Close_Panel
        End_Procedure

    End_Object

    On_Key Key_Alt+Key_O Send KeyAction of oOk_btn
    On_Key Key_Alt+Key_C Send KeyAction of oCancel_btn
End_Object

Function Edit_oAutoDeleteSelectIndexAndSegments Handle hoDDO Returns Boolean
    Handle ho
    Boolean bOK

    Move (oAutoDeleteSelectIndexAndSegments(Self)) to ho
    Set Main_DD of ho to hoDDO
    Set Server  of ho to hoDDO

    Send Popup of ho
    Get pbOK of ho to bOK

    Function_Return bOK
End_Function
